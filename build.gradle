plugins {
    id 'org.springframework.boot' version '2.6.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

subprojects {
    group = 'com.maple'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.apache.commons:commons-lang3:3.12.0'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        runtimeOnly 'mysql:mysql-connector-java'
        compileOnly 'org.projectlombok:lombok'

        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        testCompileOnly 'org.projectlombok:lombok'

        annotationProcessor 'org.projectlombok:lombok'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

        testAnnotationProcessor 'org.projectlombok:lombok'
    }
}

project(":core") {
    dependencies {
        compileOnly('org.springframework.boot:spring-boot-starter-test')
    }

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(":common") {
    dependencies {
        implementation(project(':core'))
    }

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(':api') {
    dependencies {
        implementation(project(':core'))
        implementation(project(':common'))
        implementation(project(':integration'))

        implementation('org.flywaydb:flyway-core')
    }

    bootJar {
        enabled = true
    }

    jar {
        enabled = true
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    tasks.named("bootJar") {
        archiveFileName.set("maple-boss-api.jar")
    }

    tasks.register("zip", Zip.class) {
        dependsOn("bootJar")
        archiveFileName.set("maple-boss-api.zip")

        from("build/libs/maple-boss-api.jar") { into("") }
        from("../.platform") { into(".platform") }
        from("../procfiles/api/Procfile") { into("") }
    }
}

project(':admin') {
    dependencies {
        implementation(project(':core'))
        implementation(project(':common'))
        implementation(project(':integration'))
    }

    bootJar {
        enabled = true
    }

    jar {
        enabled = true
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(':integration') {
    dependencies {
        implementation(project(':common'))

        implementation 'com.slack.api:slack-api-client:1.22.0'
    }

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}
